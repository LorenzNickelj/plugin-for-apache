plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'eclipse'
  id 'idea'
}

repositories {
  jcenter()
}

// The group will be overridden in LinkedIn builds
group=rootProject.group

configurations {
  provided
}

sourceSets {
 main {
    compileClasspath+=configurations.provided
  }
  test {
    compileClasspath+=configurations.provided
    runtimeClasspath+=configurations.provided
  }
  integTest {
    groovy.srcDir file('src/integTest/groovy')
    resources.srcDir file('src/integTest/resources')
    compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    runtimeClasspath += output + compileClasspath
  }
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile project(":hadoop-plugin")
  provided project(":hadoop-plugin")

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.10.0'

  integTestCompile 'org.spockframework:spock-core:1.3-groovy-2.4@jar'
}

task sourceJar(type: Jar) {
  description = 'An archive of the source code for the Hadoop Gradle Plugin - LinkedIn customizations'
  classifier = 'sources'
  from sourceSets.main.groovy
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for the Hadoop Gradle Plugin - LinkedIn customizations'
  classifier = 'groovydoc'
  from groovydoc.destinationDir
}

artifacts {
  archives groovydocJar, sourceJar
}

task integTest(type: Test) {
  description = 'Runs the integration tests.'
  group = 'verification'
  testClassesDirs = sourceSets.integTest.output.classesDirs
  classpath = sourceSets.integTest.runtimeClasspath
  mustRunAfter test
}

check.dependsOn integTest

gradlePlugin {
  plugins {
    liHadoopPlugin {
      id = 'li-hadoop-plugin'
      implementationClass = 'com.linkedin.gradle.lihadoop.LiHadoopPlugin'
    }
  }
}

// In LinkedIn internal builds, we pull in some company-specific resource files into this project.
boolean isLinkedInBuild = project.hasProperty('overrideBuildEnvironment')

if (isLinkedInBuild) {
  task copyLinkedInResources(type: Copy) {
    from "${rootProject.projectDir}/../resources"
    into "${project.projectDir}/src/main/resources"
  }
  compileGroovy.dependsOn copyLinkedInResources
}
